~ start
#cek status quest
if quest3Status == false:
	#cek kondisi sudah mengambil quest
	if takenQuest3 == false:
		#cek kondisi apakah sedang mengambil quest atau tidak
		if not takenQuest1 and not takenQuest2 and not takenQuest3 and not takenQuest4
			#kondisi bila belum ambil quest
			Bang Dirman: Oi anak muda, apakah kau suka tantangan?
			- Engga Dulu Deh
				Bang Dirman: Sayang Sekali
			- Tentu Saja!
				Bang Dirman: Aku Suka Semangatmu!
				Bang Dirman: Panggil aku Bang Dirman
				Bang Dirman: Aku Punya Misi Untukmmu
				Bang Dirman: Dikabarkan bahwa batik keraton dicuri oleh mahluk tak dikenal
				Bang Dirman: Batik ini sangat berharga dan tidak boleh sembarang orang memakainya
				Bang Dirman: Untuk itu, Barang siapa yang dapat mengembalikan kain batik yang telah dicuri,
				Bang Dirman: Akan diberikan hadiah batik khusus yang biasa diberikan kepada panglima perang yang telah berjasa
				Bang Dirman: Menarik bukan?
				Bang Dirman: Untuk Memulainya, pergilah kearah kanan, nanti kau akan menemukan sebuah goa dengan penjaga di sampingnya. Langsung masuk saja
				Bang Dirman: Batik tersebut seharusnya ada di suatu peti di dalam goa dungeon itu
				Bang Dirman: Berhati-hatilah untuk tidak terkena [color=red]kontak langsung dengan monster[/color]
				set takenQuest3 = true
	else:
		#kondisi bila sudah mendapatkan req item
		if reqItem3 == "1"
			Bang Dirman: Kerja bagus nak, aku akan segera melapor kepada kerajaan
			Bang Dirman: Hanya kau yang berani untuk mengambilnya kedalam sana
			Bang Dirman: Sebagai imbalan, terimalah ini hadiah dari kerajaan
			Bang Dirman: Ini merupakan kain batik bermotif [color=#965340]Gringsing[/color]
			Bang Dirman: Kain ini merupakan imbalan bagi [color=#965340]panglima perang yang sudah berjasa[/color] untuk Kerajaan
			Bang Dirman: Konon Katanya kain ini bahkan bisa menyembuhkan luka
			Bang Dirman: Memiliki motif lingkaran kecil-kecil sebagai latar dari kain dengan sebuah titik di tengah lingkaran tersebut
			Bang Dirman: Kain ini sangat berharga, jadi jangan disia siakan yaa
			Bang Dirman: Pahlawan Bima
			set takenQuest3 = false
			set quest3Status = true
			set DataBatik.batik2["isGet"] = true
			set getBatik = true
			do save_game()
			- Tunggu Bang, aku baru ingat
			=> dirmanPetarung
		else:
			#kondisi bila belum mendapatkan item
			Bang Dirman: Ada yang ingin kau tanyakan?
			- Tidak ada
				Bang Dirman: Baiklah, semoga beruntung
			- Ada yang ingin ku tanyakan
				Bang Dirman: Silakan tanyakan
				- Bagaimana Caraku untuk menemukan batiknya?
					Bang Dirman: Masuklah kedalam goa, lalu ikuti saja jalannya
					Bang Dirman: Terakhirku ingat sih jalannya tidak sulit
					Bang Dirman: Semoga, hahaha
					Bang Dirman: Aku yakin kau bisa menemukannya 
					Bang Dirman: Oh iya, sepertinya terdapat [color=#965340]kunci[/color] di suatu ruangan untuk membuka pintu ruangan harta
					Bang Dirman: Mungkin kau perlu mencarinya terlebih dahulu
				- Apa yang terjadi bila aku terkena musuh?
					Bang Dirman: Waduh!
					Bang Dirman: Rasanya akan sangat menyakitkan
					Bang Dirman: Sebaiknya jangan terkena serangan musuh sampai 3 kali
					Bang Dirman: Atau kau akan dibawa kembali ke pintu goa
else:
	#kondisi bila telah menyelesaikan quest
	if quest7Status == false
		=> dirmanPetarung
	else:
		Bang Dirman: Terimakasih atas semua pertolongan mu bim
=> END

~ petarung
if quest7Status == false
	if takenQuest7 == false
		Petarung: Tolong ... , aku butuh obat
		Bima: Baik Tunggu sebentar
		Bima: Akan ku laporkan ka Bang Dirman
	else
		Bima: Ini Bang obatnya. Aku juga sudah mengabari Bang Dirman mengenai kondisi mu
		Petarung: Terimakasih banyak nak! Kalau begitu permisi sebentar, akan saya minum
		Petarung: ... glek
		Petarung: Huhh jauh lebih baik, Aku akan istirahat sebentar di sini menuggu kondisi ku prima
		Petarung: Sebelum kau pergi, ini ambil lah kain yang kudapatkan selagi membasmi monster bersayap
		Petarung: Nama motif dari kain ini adalah [color=#965340]Sawat[/color]. Seperti nama motifnya, Sawat berarti bentuk motif [color=#965340]sepasang sayap garuda[/color] yang mengembang dan menampakan ekor di tengahnya
		Petarung: Jika dilihat, motif dari sawat terderi dari ragam hias pohon hayat, hias garuda, dan burung
		Petarung: Itu saja, terimakasih banyak karena telah menyelamatkan ku
		set takenQuest7 = false
		set quest7Status = true
		set DataBatik.batik6["isGet"] = true
		set getBatik = true
		do save_game()
else:
	Petarung: Terimakasih telah menyelamatkan ku!

=> END

~ dirmanPetarung
if reqItem7 == ""
	Bima: Bang, Aku tadi menemukan Petarung yang terluka dan butuh obat di dungeon
	Bang Dirman: Hahh? Yang benar kamu?
	Bang Dirman: Pantas saja sudah 2 hari sejak sang petarung pergi, dia tidak memberikan kabar apapun
	Bang Dirman: Bagaimana kondisinya?
	Bima: Sepertinya aman sih, tapi dia terlihat kesulitan berjalan
	Bang Dirman: Baiklah, kalau begitu aku minta tolong sekali lagi bima
	Bang Dirman: Berikan obat ini untuk si Petarung
	Bima: Baiklah bang akan ku usahakan
	set reqItem7 = "1"
	set takenQuest7 = true
else:
	Bang Dirman: Cepatlah bantu si Petarung bim
=> END 

~ item3
#kondisi bila sudah membuka peti harta
Bima: Terlihat seperti barang yang dideskripsikan oleh bang dirman
Bima: Seharusnya ini barang yang benar
Bima: Sebaiknya aku cepat melapor
set reqItem3 = "1"
=> END

~ kunciDungeon
#kondisi bila telah menemukan kunci untuk membuka pintu dungeon
Bima: Sepertinya ini merupakan kunci untuk membuka ruangan peti
Bima: Sebaiknya aku ambil
set kunciDungeon = true
=> END

~ pintuDungeon
#kondisi interaksi dengan pintu yang terkunci
if pintuDungeon == true
	=> END
else
	if kunciDungeon == false:
		Bima: Pintu ini terkunci, sepertinya kuncinya ada disekitar sini
		Bima: Sebaiknya aku ambil
	else:
		Bima: Terbuka!
		Bima: Kuncinya ternyata cocok
		set pintuDungeon = true
		=> END
	

